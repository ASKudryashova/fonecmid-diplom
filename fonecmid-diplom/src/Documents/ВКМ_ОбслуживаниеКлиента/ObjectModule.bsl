#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	АбонентскийДоговор = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание");
	
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ВидДоговора, ВКМ_ПериодДействияНачало, ВКМ_ПериодДействияКонец");
	
	Если Данные.ВидДоговора <> АбонентскийДоговор Тогда
		
		ТекстСообщения = СтрШаблон("Указан неверный вид договора: ""%1"". Документ предназначен только для договоров вида ""%2"".", 
			Данные.ВидДоговора, АбонентскийДоговор);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		Отказ = Истина;
	
	ИначеЕсли Дата < Данные.ВКМ_ПериодДействияНачало Или Дата > Данные.ВКМ_ПериодДействияКонец Тогда
		
		ТекстСообщения = СтрШаблон("Дата документа не находится в пределах периода действия договора. Период действия договора: с %1 по %2.",
		 Формат(Данные.ВКМ_ПериодДействияНачало, "ДФ=dd.MM.yyyy;"), Формат(Данные.ВКМ_ПериодДействияКонец, "ДФ=dd.MM.yyyy;"));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		Отказ = Истина;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК ЧасыКОплатеКлиенту,
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист
		|ПОМЕСТИТЬ ВТ_Документ
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = ВКМ_ОбслуживаниеКлиента.Ссылка
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиента.Договор.ВКМ_СтоимостьЧасаРаботы,
		|	ВКМ_ОбслуживаниеКлиента.Специалист
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
		|ПОМЕСТИТЬ ВТ_УсловияОплаты
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Специалист) КАК
		|		ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Документ.СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
		|	ВТ_Документ.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту,
		|	ВТ_УсловияОплаты.ПроцентОтРабот КАК ПроцентОтРабот
		|ИЗ
		|	ВТ_Документ КАК ВТ_Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УсловияОплаты КАК ВТ_УсловияОплаты
		|		ПО ВТ_Документ.Специалист = ВТ_УсловияОплаты.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Специалист", Специалист);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий()Цикл;
	
		Если ВыборкаДетальныеЗаписи.ПроцентОтРабот = Null Тогда
			
			ТекстСообщения = СтрШаблон("Сотруднику %1 не определены условия оплаты по проценту от работ", Специалист);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
			Возврат; 
	
		Иначе
			
			//регистр ВКМ_ВыполненныеКлиентуРаботы
			Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
			Движение.Период = ДатаПроведенияРабот;
			Движение.Клиент = Клиент;
			Движение.Договор = Договор;
			Движение.КоличествоЧасов = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту;
			Движение.СуммаКОплате = Движение.КоличествоЧасов * ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы;
			
			//регистр ВКМ_ВыполненныеСотрудникомРаботы
			Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
			Движение.Период = Дата;
			Движение.Сотрудник = Специалист;
			Движение.ЧасовКОплате = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту;
			Движение.СуммаКОплате = (ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту * ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы * ВыборкаДетальныеЗаписи.ПроцентОтРабот) / 100;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ДатаПроведенияРабот, ВремяНачалаРаботПлан, Специалист"); 
	
	Если РеквизитыОбъекта.ДатаПроведенияРабот <> ДатаПроведенияРабот
		Или РеквизитыОбъекта.ВремяНачалаРаботПлан <> ВремяНачалаРаботПлан
		Или РеквизитыОбъекта.Специалист <> Специалист Тогда
			 
		ЗаказИзменен = Истина;
		
	Иначе
		
		ЗаказИзменен = Ложь;
		
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЗаказИзменен", ЗаказИзменен);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеСвойства.ЭтоНовый Тогда

		ТекстСообщения = СтрШаблон("Новый заказ № %1, Исполнитель %2. Дата: %3, интервал работ: %4 - %5", Номер, 
			Специалист, Формат(ДатаПроведенияРабот, "ДЛФ=D"), Формат(ВремяНачалаРаботПлан, "ДЛФ=T"), 
			Формат(ВремяОкончанияРаботПлан, "ДЛФ=T"));
		ДокОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		ДокОбъект.ТекстСообщения = ТекстСообщения;
		ДокОбъект.Записать();

	ИначеЕсли ДополнительныеСвойства.ЗаказИзменен Тогда

		ТекстСообщения = СтрШаблон("Заказ № %1 изменен. Исполнитель %2. Дата: %3, интервал работ: %4 - %5", Номер,
			Специалист, Формат(ДатаПроведенияРабот, "ДЛФ=D"), Формат(ВремяНачалаРаботПлан, "ДЛФ=T"), 
			Формат(ВремяОкончанияРаботПлан, "ДЛФ=T"));
		ДокОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		ДокОбъект.ТекстСообщения = ТекстСообщения;
		ДокОбъект.Записать();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
