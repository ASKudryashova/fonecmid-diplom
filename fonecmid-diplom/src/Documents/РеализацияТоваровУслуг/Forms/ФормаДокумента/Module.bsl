
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//ВКМ Кудряшова А.С. Добавление комнады "Заполнить"	
	КомандаЗаполнить = Команды.Добавить("ВКМ_Заполнить");
	КомандаЗаполнить.Заголовок = "Заполнить";
	КомандаЗаполнить.Действие = "ВКМ_Заполнить";

	КнопкаКоманды = Элементы.Вставить("ВКМ_Заполнить", Тип("КнопкаФормы"), КоманднаяПанель);
	КнопкаКоманды.ИмяКоманды = "ВКМ_Заполнить";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	
	//ВКМ 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

//ВКМ Кудряшова А.С. Добавление действия к команде "Заполнить"	
&НаКлиенте
Процедура ВКМ_Заполнить(Команда)

	Если ЗначениеЗаполнено(Объект.Договор) Тогда
		ВКМ_ЗаполнитьНаСервере();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать договор");
	КонецЕсли;

КонецПроцедуры
//ВКМ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

//ВКМ Кудряшова А.С. продолжение команды "Заполнить"
&НаСервере
Процедура ВКМ_ЗаполнитьНаСервере()
	
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Договор, "ВидДоговора, ВКМ_ПериодДействияНачало, ВКМ_ПериодДействияКонец");
		
	Если Данные.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		
		Если Данные.ВКМ_ПериодДействияНачало <= Объект.Дата И Данные.ВКМ_ПериодДействияКонец >= Объект.Дата Тогда
			ДокументОбъект = РеквизитФормыВЗначение("Объект");
			ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		Иначе
			ТекстСообщения = СтрШаблон("Дата документа не находится в пределах периода действия договора. Период действия договора: с %1 по %2.",
				Формат(Данные.ВКМ_ПериодДействияНачало, "ДФ=dd.MM.yyyy;"), Формат(Данные.ВКМ_ПериодДействияКонец, "ДФ=dd.MM.yyyy;"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	
	Иначе
		ТекстСообщения = СтрШаблон("Указан неверный вид договора: ""%1"". Автозаполнение документа предназначено только для договоров вида ""%2"".", 
			Данные.ВидДоговора, Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
	КонецЕсли;

КонецПроцедуры 
//ВКМ

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
