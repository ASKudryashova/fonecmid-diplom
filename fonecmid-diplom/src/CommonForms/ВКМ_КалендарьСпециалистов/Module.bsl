
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = НачалоДня(ТекущаяДатаСеанса());
	КонецПериода = КонецДня(ТекущаяДатаСеанса()) + 259200;
	
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода);
	
	ОбновитьПланировщик();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОбслуживаниеКлиента" Тогда
		ОбновитьПланировщик();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачалоДняВыполненияРабот = НачалоДня(Начало);
	ВремяНачалаРабот = '00010101' + (Начало - НачалоДняВыполненияРабот);
	ВремяОкончанияРабот = '00010101' + (Конец - НачалоДняВыполненияРабот); 
	
	ЗначенияЗаполнения = Новый Структура;
	
	ЗначенияЗаполнения.Вставить("Дата", ТекущаяДата());
	ЗначенияЗаполнения.Вставить("ДатаПроведенияРабот", НачалоДня(Начало));
	ЗначенияЗаполнения.Вставить("ВремяНачалаРаботПлан", ВремяНачалаРабот);
	ЗначенияЗаполнения.Вставить("ВремяОкончанияРаботПлан", ВремяОкончанияРабот);
	ЗначенияЗаполнения.Вставить("Специалист", ЗначенияИзмерений.Получить("Специалисты"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПланировщик()
	
	Планировщик.Элементы.Очистить();
	ИзмеренияПланировщика  = Планировщик.Измерения;
    ИзмеренияПланировщика.Очистить();
    
    ИзмерениеСпециалисты = ИзмеренияПланировщика.Добавить("Специалисты");
	
	ВыполнениеРабот = Документы.ВКМ_ОбслуживаниеКлиента.Выбрать();
	
	Пока ВыполнениеРабот.Следующий() Цикл
		
		Если ИзмерениеСпециалисты.Элементы.Найти(ВыполнениеРабот.Специалист) = Неопределено Тогда
			ИзмерениеСпециалисты.Элементы.Добавить(ВыполнениеРабот.Специалист, ВыполнениеРабот.Специалист.Наименование);	
		КонецЕсли;
		
		СоответствиеЗначений = Новый Соответствие;
		СоответствиеЗначений.Вставить("Специалисты", ВыполнениеРабот.Специалист);		
		
		МассивРабот = ВыполнениеРабот.ВыполненныеРаботы.ВыгрузитьКолонку("ОписаниеРабот");
	
		ВремяНачалаРабот = ВыполнениеРабот.ДатаПроведенияРабот + (ВыполнениеРабот.ВремяНачалаРаботПлан - '00010101');
		ВремяОкончанияРабот = ВыполнениеРабот.ДатаПроведенияРабот + (ВыполнениеРабот.ВремяОкончанияРаботПлан - '00010101');
		
		ЭлементПланировщика = Планировщик.Элементы.Добавить(ВремяНачалаРабот, ВремяОкончанияРабот);
		ЭлементПланировщика.Значение = ВыполнениеРабот.Ссылка;
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
		ЭлементПланировщика.Текст = "Клиент " + ВыполнениеРабот.Клиент + ": " + СтрСоединить(МассивРабот, "; ");
			
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти